
Multichar_Symbols

! Nominal tags:
%<n%>
%<np%>
%<sg%>
%<pl%>
%<ind%>
%<def%>
%<adj%>
%<ut%>
%<nt%>
%<prop%>
%<nom%>
%<int%>

! Pronominal additions:
%<prn%>
%<pers%>
%<p1%>
%<p2%>
%<p3%>
%<nom%>
%<acc%>
%<gen%>
%<refl%>
%<fem%>
%<masc%>
%<refgen%>

%<mf%>
%<m%>
%<f%>

! Verb tags:
%<vblex%>
%<inf%>
%<imp%>
%<pres%>
%<past%>
%<pp%>

! Tags for other POSes
%<adv%>
%<cnjcoo%>
%<det%>
%<pr%>
%<num%>


! Punctuation
%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side)
%<lquot%>     ! Quote marker (left hand side)
%<rpar%>      ! Parenthetical marker (right hand side)
%<lpar%>      ! Parenthetical marker (left hand side)



%  

LEXICON Root

Nouns ;
Adjectives ;
Adverbs ;
Conjunctions ;
Determiners ;
Prepositions ;
Pronouns ;
Propernouns ;
Verbs ;
Numerals ;
Punctuation ;
Interjections ;

LEXICON N1 ! Utrum
%<n%>%<ut%>%<sg%>%<ind%>%<nom%>: # ;
%<n%>%<ut%>%<sg%>%<def%>%<nom%>:en # ;
%<n%>%<ut%>%<pl%>%<ind%>%<nom%>:ar # ;
%<n%>%<ut%>%<pl%>%<def%>%<nom%>:arna # ;

LEXICON N2 ! Neutrum
%<n%>%<nt%>%<sg%>%<ind%>%<nom%>:er  # ;
%<n%>%<nt%>%<sg%>%<def%>%<nom%>:ret # ;
%<n%>%<nt%>%<pl%>%<ind%>%<nom%>:er  # ;
%<n%>%<nt%>%<pl%>%<def%>%<nom%>:ren  # ;

LEXICON N3 ! Utrum on -a
%<n%>%<ut%>%<sg%>%<ind%>%<nom%>:a  # ;
%<n%>%<ut%>%<sg%>%<def%>%<nom%>:an   # ;
%<n%>%<ut%>%<pl%>%<ind%>%<nom%>:or # ;
%<n%>%<ut%>%<pl%>%<def%>%<nom%>:orna # ;

LEXICON N4 ! Utrum hand sg
%<n%>%<ut%>%<sg%>%<ind%>%<nom%>:and  # ;
%<n%>%<ut%>%<sg%>%<def%>%<nom%>:anden   # ;

LEXICON N5 ! Utrum hand pl
%<n%>%<ut%>%<pl%>%<ind%>%<nom%>:änder # ;
%<n%>%<ut%>%<pl%>%<def%>%<nom%>:änderna # ;

LEXICON N6 ! Neutrum vatten
%<n%>%<nt%>%<sg%>%<ind%>%<nom%>:en # ;
%<n%>%<nt%>%<sg%>%<def%>%<nom%>:net # ;
%<n%>%<nt%>%<pl%>%<ind%>%<nom%>:en # ;
%<n%>%<nt%>%<pl%>%<def%>%<nom%>:nen # ;

LEXICON N7 ! Utrum syster
%<n%>%<ut%>%<sg%>%<ind%>%<nom%>:er # ;
%<n%>%<ut%>%<sg%>%<def%>%<nom%>:ern # ;
%<n%>%<ut%>%<pl%>%<ind%>%<nom%>:rar # ;
%<n%>%<ut%>%<pl%>%<def%>%<nom%>:rarna # ;

LEXICON N8 ! Utrum pojke
%<n%>%<ut%>%<sg%>%<ind%>%<nom%>:e # ;
%<n%>%<ut%>%<sg%>%<def%>%<nom%>:en # ;
%<n%>%<ut%>%<pl%>%<ind%>%<nom%>:ar # ;
%<n%>%<ut%>%<pl%>%<def%>%<nom%>:arna # ;

LEXICON N9 ! Utrum fågel
%<n%>%<ut%>%<sg%>%<ind%>%<nom%>: # ;
%<n%>%<ut%>%<sg%>%<def%>%<nom%>:n # ;
%<n%>%<ut%>%<pl%>%<ind%>%<nom%>:ar # ;
%<n%>%<ut%>%<pl%>%<def%>%<nom%>:arna # ;

LEXICON N10 ! Neutrum öga
%<n%>%<nt%>%<sg%>%<ind%>%<nom%>:an # ;
%<n%>%<nt%>%<sg%>%<def%>%<nom%>:at # ;
%<n%>%<nt%>%<pl%>%<ind%>%<nom%>:on # ;
%<n%>%<nt%>%<pl%>%<def%>%<nom%>:onen # ;

LEXICON N11 ! Utrum
%<n%>%<ut%>%<sg%>%<ind%>%<nom%>: # ;
%<n%>%<ut%>%<sg%>%<def%>%<nom%>:en # ;
%<n%>%<ut%>%<pl%>%<ind%>%<nom%>:er # ;
%<n%>%<ut%>%<pl%>%<def%>%<nom%>:erna # ;


LEXICON Adj-lex
%<adj%>%<sg%>%<ind%>%<ut%>:  # ;
%<adj%>%<sg%>%<ind%>%<nt%>:t # ;
%<adj%>%<pl%>:a  # ;
%<adj%>%<def%>:a # ;

LEXICON Adj-god-lex
d%<adj%>%<sg%>%<ind%>%<ut%>:d # ;
d%<adj%>%<sg%>%<ind%>%<nt%>:tt # ;
d%<adj%>%<pl%>:da  # ;
d%<adj%>%<def%>:da # ;

LEXICON Adj-liten-lex
ten%<adj%>%<sg%>%<ind%>%<ut%>:ten # ;
ten%<adj%>%<sg%>%<ind%>%<nt%>:tet # ;
ten%<adj%>%<pl%>:lla  # ;
ten%<adj%>%<def%>:lla # ;


LEXICON Adv-lex
%<adv%>: # ;

LEXICON Conj-lex
%<cnjcoo%>: # ;

LEXICON Det-en-lex
%<det%>%<ind%>%<ut%>:n # ;
%<det%>%<ind%>%<nt%>:tt # ;

LEXICON Det-den-lex
%<det%>%<def%>%<ut%>:n # ;
%<det%>%<def%>%<nt%>:t # ;


LEXICON Int-lex
%<int%>: # ;


LEXICON Prep-lex
%<pr%>: # ;

LEXICON Proper-lex
%<np%>%<sg%>%<ind%>%<nom%>: # ;


LEXICON PRON_INGEN
%<prn%>%<sg%>%<ut%>:en # ;
%<prn%>%<sg%>%<nt%>:et # ;
%<prn%>%<pl%>:a # ;

LEXICON PRON_NÅGON
%<prn%>%<sg%>%<ut%>:on # ;
%<prn%>%<sg%>%<nt%>:ot # ;
%<prn%>%<pl%>:ra # ;

LEXICON PRON_ANNAN
%<prn%>%<sg%>%<ut%>:nan # ;
%<prn%>%<sg%>%<nt%>:nat # ;
%<prn%>%<pl%>:dra # ;



LEXICON Num-lex
%<num%>: # ;

LEXICON V-vara-lex
%<vblex%>%<inf%>:vara  # ;
%<vblex%>%<imp%>:var  # ;
%<vblex%>%<pres%>:är  # ;
%<vblex%>%<past%>:var # ;
%<vblex%>%<pp%>:varit # ;

LEXICON V-er-lex
%<vblex%>%<inf%>:a   # ;
%<vblex%>%<imp%>:  # ;
%<vblex%>%<pres%>:er # ;
%<vblex%>%<past%>:te # ;
%<vblex%>%<pp%>:t  # ;

LEXICON V-ar-lex
%<vblex%>%<inf%>:a    # ;
%<vblex%>%<imp%>:  # ;
%<vblex%>%<pres%>:ar  # ;
%<vblex%>%<past%>:ade # ;
%<vblex%>%<pp%>:at  # ;

LEXICON V-veta-lex
%<vblex%>%<inf%>:eta    # ;
%<vblex%>%<imp%>:et  # ;
%<vblex%>%<pres%>:et    # ;
%<vblex%>%<past%>:isste # ;
%<vblex%>%<pp%>:isst  # ;

LEXICON V-kunna-lex
%<vblex%>%<inf%>:unna   # ;
%<vblex%>%<pres%>:an    # ;
%<vblex%>%<past%>:unde  # ;
%<vblex%>%<pp%>:unnat # ;

LEXICON V-ha-lex
%<vblex%>%<inf%>:    # ;
%<vblex%>%<imp%>:  # ;
%<vblex%>%<pres%>:r  # ;
%<vblex%>%<past%>:de # ;
%<vblex%>%<pp%>:ft # ;

LEXICON V-se-lex
%<vblex%>%<inf%>:e    # ;
%<vblex%>%<imp%>:e  # ;
%<vblex%>%<pres%>:er  # ;
%<vblex%>%<past%>:åg  # ;
%<vblex%>%<pp%>:ett # ;

LEXICON V-gå-lex
%<vblex%>%<inf%>:å    # ;
%<vblex%>%<imp%>:å  # ;
%<vblex%>%<pres%>:år  # ;
%<vblex%>%<past%>:ick # ;
%<vblex%>%<pp%>:ått # ;

LEXICON V-ta-lex
%<vblex%>%<inf%>:a     # ;
%<vblex%>%<imp%>:a  # ;
%<vblex%>%<pres%>:ar   # ;
%<vblex%>%<past%>:og   # ;
%<vblex%>%<pp%>:agit # ;

LEXICON V-äta-lex
%<vblex%>%<inf%>:äta   # ;
%<vblex%>%<imp%>:äta  # ;
%<vblex%>%<pres%>:äter # ;
%<vblex%>%<past%>:åt   # ;
%<vblex%>%<pp%>:ätit # ;

LEXICON V-dricka-lex
%<vblex%>%<inf%>:icka   # ;
%<vblex%>%<imp%>:ick  # ;
%<vblex%>%<pres%>:icker # ;
%<vblex%>%<past%>:ack   # ;
%<vblex%>%<pp%>:uckit # ;

LEXICON V-komma-lex
%<vblex%>%<inf%>:ma   # ;
%<vblex%>%<imp%>:  # ;
%<vblex%>%<pres%>:mer # ;
%<vblex%>%<past%>:    # ;
%<vblex%>%<pp%>:mit # ;

LEXICON V-vilja-lex
%<vblex%>%<inf%>:ilja  # ;
%<vblex%>%<pres%>:ill  # ;
%<vblex%>%<past%>:ille # ;
%<vblex%>%<pp%>:elat # ;

LEXICON V-måsta-lex
%<vblex%>%<inf%>:a  # ;
%<vblex%>%<pres%>:e # ;
%<vblex%>%<past%>:e # ;
%<vblex%>%<pp%>: # ;

LEXICON V-göra-lex
%<vblex%>%<inf%>:öra   # ;
%<vblex%>%<imp%>:ö  # ;
%<vblex%>%<pres%>:ör   # ;
%<vblex%>%<past%>:jorde # ;
%<vblex%>%<pp%>:jort # ;

LEXICON V-tro-lex
%<vblex%>%<inf%>:     # ;
%<vblex%>%<imp%>:  # ;
%<vblex%>%<pres%>:r   # ;
%<vblex%>%<past%>:dde # ;
%<vblex%>%<pp%>:tt # ;

LEXICON V-smyga-lex
%<vblex%>%<inf%>:yga   # ;
%<vblex%>%<pres%>:yger # ;
%<vblex%>%<past%>:ög   # ;
%<vblex%>%<pp%>:ugit # ;

LEXICON V-sova-lex
%<vblex%>%<inf%>:a   # ;
%<vblex%>%<pres%>:er # ;
%<vblex%>%<past%>:   # ;
%<vblex%>%<pp%>:it # ;

LEXICON V-sitta-lex
%<vblex%>%<inf%>:itta   # ;
%<vblex%>%<pres%>:itter # ;
%<vblex%>%<past%>:att   # ;
%<vblex%>%<pp%>:uttit # ;

LEXICON V-säga-lex
%<vblex%>%<inf%>:äga   # ;
%<vblex%>%<imp%>:äg   # ;
%<vblex%>%<pres%>:äger # ;
%<vblex%>%<past%>:ade   # ;
%<vblex%>%<pp%>:agt # ;

LEXICON V-springa-lex
%<vblex%>%<inf%>:inga   # ;
%<vblex%>%<imp%>:ing   # ;
%<vblex%>%<pres%>:inger # ;
%<vblex%>%<past%>:ang   # ;
%<vblex%>%<pp%>:ungit # ;

LEXICON V-höra-lex
%<vblex%>%<inf%>:a   # ;
%<vblex%>%<imp%>:   # ;
%<vblex%>%<pres%>: # ;
%<vblex%>%<past%>:de   # ;
%<vblex%>%<pp%>:t # ;


LEXICON Nouns

hund             N1 "dog"       ;
hus              N2 "house"     ;
träd             N2 "tree"      ;
svar             N2 "answer"    ;
år               N2 "year"      ;
trädgård         N1 "garden"    ;
mamma:mamm       N3 "mother"    ;
katt             N1 "cat"       ;
väg              N1 "way, road" ;
flicka:flick     N3 "girl"      ;
dag              N1 "day"       ;
väder:väd     N2 "weather"   ;
fönster:fönst N2 "window"    ;
stol             N1 "chair"     ;
ljud             N2 "sound"     ;
bord             N2 "table"     ;
barn             N2 "child"     ;
djur             N2 "animal"    ;
huvud            N2 "head"      ;
folk             N2 "people"    ;
hand:h		 N4 "hand"	;
hand:h		 N5 "hand"	;
vatten:vatt	 N6 "water"	;
syster:syst	 N7 "sister"	;
pojke:pojk       N8 "boy"       ;
fågel            N9 "bird"      ;
fråga:fråg       N3 "question"  ;
öga:ög           N10 "eye"      ;
tid 		 N11 ;
lek          N1 ;

LEXICON Adverbs

inte     Adv-lex ;
också    Adv-lex ;
när      Adv-lex ;
även     Adv-lex ;
då       Adv-lex ;
där      Adv-lex ;
nu       Adv-lex ;
aldrig   Adv-lex ;
alltid   Adv-lex ;
fast     Adv-lex ;
utomhus  Adv-lex ;
sedan    Adv-lex ;
i% går   Adv-lex ;
i% dag   Adv-lex ;
igen     Adv-lex ;
troligen Adv-lex ;
så       Adv-lex ;
sakta    Adv-lex ;
tillsammans  Adv-lex ;
naturligtvis Adv-lex ;
fortfarande  Adv-lex ;
överallt     Adv-lex ;
där% borta   Adv-lex ;
här          Adv-lex ;
snabbt       Adv-lex ;
mycket       Adv-lex ;
varför       Adv-lex ;
noggrant     Adv-lex ;
utanför      Adv-lex ;
närmare      Adv-lex ;
omkring      Adv-lex ;
självklart   Adv-lex ;
kanske       Adv-lex ;
säkert       Adv-lex ;
väldigt      Adv-lex ;
tydligt      Adv-lex ;
var	     Adv-lex ;
inne         Adv-lex ;
även% om     Adv-lex ;
in 	     Adv-lex ;

LEXICON Adj-bra
%<adj%>%<sg%>%<ind%>%<ut%>:bra # ;

LEXICON Adjectives

bra:0          Adj-bra ;
väldig         Adj-lex ;
tydlig         Adj-lex ;
kall           Adj-lex ;
varm           Adj-lex ;
vacker:vacker Adj-lex ;
glad:glad     Adj-lex ;
gammal:gammal Adj-lex ;
stor           Adj-lex ;
snabb          Adj-lex ;
go	       Adj-god-lex ; 
färdig         Adj-lex ;
li             Adj-liten-lex ;

LEXICON Conjunctions

och   Conj-lex ;
men   Conj-lex ;
att   Conj-lex ;
eller Conj-lex ;

LEXICON Determiners
 en:e  Det-en-lex ;
den:de Det-den-lex ;

LEXICON Interjections
ja	Int-lex ;
nej	Int-lex ;

LEXICON Prepositions
i       Prep-lex ;
på      Prep-lex ;
över    Prep-lex ;
under   Prep-lex ;
för     Prep-lex ;
ut      Prep-lex ;
till    Prep-lex ;
med     Prep-lex ;
framför Prep-lex ;
bakom   Prep-lex ;
om      Prep-lex ;
att     Prep-lex ;
genom   Prep-lex ;
efter   Prep-lex ;
från    Prep-lex ;
inuti   Prep-lex ;
inne    Prep-lex ;
in      Prep-lex ;
före    Prep-lex ;

LEXICON Propernouns
Mary  Proper-lex ;
James Proper-lex ;
Mari  Proper-lex ;
Jaakko Proper-lex ;

LEXICON PRON_JAG
%<prn%>%<pers%>%<p1%>%<sg%>%<nom%>:jag  # ;
%<prn%>%<pers%>%<p1%>%<sg%>%<acc%>:mig   # ;
%<prn%>%<pers%>%<p1%>%<sg%>%<gen%>:min   # ;

LEXICON PRON_DU
%<prn%>%<pers%>%<p2%>%<sg%>%<nom%>:du   # ;
%<prn%>%<pers%>%<p2%>%<sg%>%<acc%>:dig   # ;
%<prn%>%<pers%>%<p2%>%<sg%>%<gen%>:din   # ;

LEXICON PRON_HON
%<prn%>%<pers%>%<p3%>%<f%>%<sg%>%<nom%>:hon  # ;
%<prn%>%<pers%>%<p3%>%<f%>%<sg%>%<acc%>:henne # ;
%<prn%>%<pers%>%<p3%>%<f%>%<sg%>%<gen%>:hennes # ;

LEXICON PRON_HAN
%<prn%>%<pers%>%<p3%>%<m%>%<sg%>%<nom%>:han  # ;
%<prn%>%<pers%>%<p3%>%<m%>%<sg%>%<acc%>:honom # ;
%<prn%>%<pers%>%<p3%>%<m%>%<sg%>%<gen%>:hans   # ;
 
LEXICON PRON_VI
%<prn%>%<pers%>%<p1%>%<pl%>%<nom%>:vi   # ;
%<prn%>%<pers%>%<p1%>%<pl%>%<acc%>:oss   # ;
%<prn%>%<pers%>%<p1%>%<pl%>%<acc%>:vårt  # ;


LEXICON PRON_NI
%<prn%>%<pers%>%<p2%>%<pl%>%<nom%>:ni   # ;
%<prn%>%<pers%>%<p2%>%<pl%>%<acc%>:er    # ;

LEXICON PRON_DE
%<prn%>%<pers%>%<p3%>%<mf%>%<pl%>%<nom%>:de   # ;
%<prn%>%<pers%>%<p3%>%<mf%>%<pl%>%<acc%>:dem   # ;
%<prn%>%<pers%>%<p3%>%<mf%>%<pl%>%<gen%>:deras  # ;

LEXICON Pronouns
jag:0 PRON_JAG ;
du:0  PRON_DU ;
hon:0 PRON_HON ;
han:0 PRON_HAN ;
vi:0 PRON_VI ;
ni:0 PRON_NI ;
de:0 PRON_DE ;

den%<prn%>%<sg%>%<ut%>:den   # ;
den%<prn%>%<sg%>%<nt%>:det   # ;
sig%<prn%>%<refl%>%<acc%>:sig # ;

 ingen:ing PRON_INGEN ;
 någon:någ PRON_NÅGON ;
 annan:an PRON_ANNAN ;
 båda PRON ;

vad PRON ;

LEXICON Numerals
två  Num-lex ;
tre  Num-lex ;
fyra Num-lex ;
fem  Num-lex ;
sex  Num-lex ;
sju  Num-lex ;
åtta Num-lex ;
nio  Num-lex ;
tio  Num-lex ;

LEXICON Verbs
! Irregular and strond verbs:
   vara:       V-vara-lex ;
   veta:v      V-veta-lex ;
  kunna:k      V-kunna-lex ;
 dricka:dr     V-dricka-lex ;
    äta:       V-äta-lex ;
     ha        V-ha-lex ;
     se:s      V-se-lex ;
     gå:g      V-gå-lex ;
     få:f      V-gå-lex ;
     ta:t      V-ta-lex ;
 komma:kom     V-komma-lex ;
  vilja:v      V-vilja-lex ;
 måsta:måst    V-måsta-lex ;
  göra:g       V-göra-lex ;
    tro	       V-tro-lex ;
  smyga:sm     V-smyga-lex ;
  sova:sov     V-sova-lex ;
  sitta:s      V-sitta-lex ;
   säga:s      V-säga-lex ;
 springa:spr   V-springa-lex ;
  höra:hör     V-höra-lex ;

gömma% sig%<vblex%>%<inf%>:gömma% sig # ;
gömma% sig%<vblex%>%<pres%>:gömmer% sig # ;
gömma% sig%<vblex%>%<past%>:gömde% sig # ;
gömma% sig%<vblex%>%<pp%>:gömt% sig # ;

! Regular verbs:
  tänka:tänk   V-er-lex ;
skratta:skratt V-ar-lex ;
   leka:lek    V-ar-lex ;
  hitta:hitt   V-ar-lex ;
  svara:svar   V-ar-lex ;
berätta:berätt V-ar-lex ;
   leta:let    V-ar-lex ;
  titta:titt   V-ar-lex ;
   tala:tal    V-ar-lex ;
  fråga:fråg   V-ar-lex ;
  räkna:räkn   V-ar-lex ;
  prata:prat   V-ar-lex ;
  älska:älsk   V-ar-lex ;
  gilla:gill   V-ar-lex ;
  kolla:koll   V-ar-lex ;
 lyssna:lyssn  V-ar-lex ;
  sluta:slut   V-ar-lex ;
  prova:prov   V-ar-lex ;
   kika:kik    V-ar-lex ;
tycka:tyck     V-er-lex ;

!===================!
 LEXICON Punctuation
!===================!


%.%<sent%>:%. # ;
%.%.%<sent%>:%.%. # ;
%.%.%.%<sent%>:%.%.%. # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%’%<apos%>:%’ # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%»%<lquot%>:%» # ;
%”%<rquot%>:%” # ;
%”%<lquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
%\%<sent%>:%\ # ;
%/%<sent%>:%/ # ;

